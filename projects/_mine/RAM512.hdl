// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16-bit wide.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = RAM512[address(t)](t)
 *     Write: If load(t-1) then RAM512[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory
 * location specified by address. If load == 1, the in value is loaded
 * into the memory location specified by address.  This value becomes
 * available through the out output starting from the next time step.
 */

CHIP RAM512 {
    IN  in[16], load, address[9];
    OUT out[16];

    PARTS:

        // fork load signal
        DMux8Way(
            in=load, sel=address[0..2],
            a=load0, b=load1, c=load2, d=load3,
            e=load4, f=load5, g=load6, h=load7);

        // storage
        RAM64(in=in, address=address[3..8], load=load0, out=word0);
        RAM64(in=in, address=address[3..8], load=load1, out=word1);
        RAM64(in=in, address=address[3..8], load=load2, out=word2);
        RAM64(in=in, address=address[3..8], load=load3, out=word3);
        RAM64(in=in, address=address[3..8], load=load4, out=word4);
        RAM64(in=in, address=address[3..8], load=load5, out=word5);
        RAM64(in=in, address=address[3..8], load=load6, out=word6);
        RAM64(in=in, address=address[3..8], load=load7, out=word7);

        // join signal
        Mux8Way16(
            a=word0, b=word1, c=word2, d=word3,
            e=word4, f=word5, g=word6, h=word7,
            sel=address[0..2], out=out);
}
